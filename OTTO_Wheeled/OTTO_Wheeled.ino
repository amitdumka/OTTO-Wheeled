
//----------------------------------------------------------------
//-- Zowi basic firmware v2 adapted to Otto
//-- (c) BQ. Released under a GPL licencse
//-- 04 December 2015
//-- Authors:  Anita de Prado: ana.deprado@bq.com
//--           Jose Alberca:   jose.alberca@bq.com
//--           Javier Isabel:  javier.isabel@bq.com
//--           Juan Gonzalez (obijuan): juan.gonzalez@bq.com
//--           Irene Sanz : irene.sanz@bq.com
//-----------------------------------------------------------------
//-- Experiment with all the features that Otto has thanks to Zowi!
//-----------------------------------------------------------------
//-- AUGUST 2017: modified for a WHEELED OTTO with MAX7219 MATRIX LED module
//-- Two continous rotation serovs are used
//-- Jason Snow
//-----------------------------------------------------------------

//#include <Servo.h> 
//#include <Oscillator.h>
//#include <EEPROM.h>
#include <BatReader.h>
#include <US.h>
#include "MaxMatrix.h"
MaxMatrix ledmatrix=MaxMatrix(12,10,11, 1); //PIN  12=DIN, PIN 10=CS, PIN 11=CLK

//-- Library to manage external interruptions
#include <EnableInterrupt.h> 

//-- Library to manage serial commands
#include <OttoSerialCommand.h>
OttoSerialCommand SCmd;  //The SerialCommand object

//-- Otto Library
#include <OttoW.h>
OttoW OttoW;  //This is Otto!
 
//---------------------------------------------------------
//-- First step: Configure the pins where the servos are attached
/*
         --------------- 
        |     O   O     |
        |---------------|
YR 3==> |               | <== YL 2
         --------------- 

*/

  #define PIN_YL 2 //servo[0]
  #define PIN_YR 3 //servo[1]
  

//---Otto Buttons
#define PIN_SecondButton 6
#define PIN_ThirdButton 7
// buzzer connected to pin 13

///////////////////////////////////////////////////////////////////
//-- Global Variables -------------------------------------------//
///////////////////////////////////////////////////////////////////

const char programID[]="Otto_todo"; //Each program will have a ID

const char name_fac='$'; //Factory name
const char name_fir='#'; //First name

//-- Movement parameters
int T=1000;              //Initial duration of movement
int moveId=0;            //Number of movement
int moveSize=15;         //Asociated with the height of some movements

//---------------------------------------------------------
//-- Otto has 5 modes:
//--    * MODE = 0: Otto is awaiting  
//--    * MODE = 1: Dancing mode!  
//--    * MODE = 2: Obstacle detector mode  
//--    * MODE = 3: Noise detector mode   
//--    * MODE = 4: OttoPAD or any Teleoperation mode (listening SerialPort). 
//---------------------------------------------------------
volatile int MODE=2; //State of Otto in the principal state machine. 

volatile bool buttonPushed=false;  //Variable to remember when a button has been pushed
volatile bool buttonAPushed=false; //Variable to remember when A button has been pushed
volatile bool buttonBPushed=false; //Variable to remember when B button has been pushed

unsigned long previousMillis=0;

int randomDance=0;
int randomSteps=0;

bool obstacleDetected = false;


///////////////////////////////////////////////////////////////////
//-- Setup ------------------------------------------------------//
///////////////////////////////////////////////////////////////////
void setup(){

  //Serial communication initialization
  Serial.begin(19200);  

  pinMode(PIN_SecondButton,INPUT); //D6 - ensure pull-down resistors are used
  pinMode(PIN_ThirdButton,INPUT); //D7 - ensure pull-down resistors are used
  
  //Set the servo pins
  OttoW.init(PIN_YL,PIN_YR,true);
 
  OttoW.setTrims(0,-2); // PLACE YOUR CALIBRATION VALUES HERE  =  Left Servo , Right servo 
  
// set up Matrix display
  ledmatrix.init();
  ledmatrix.setIntensity(1);
  //Set a random seed
  randomSeed(analogRead(A6));

  //Interrumptions
  enableInterrupt(PIN_SecondButton, secondButtonPushed, RISING);
  enableInterrupt(PIN_ThirdButton, thirdButtonPushed, RISING);

  //Setup callbacks for SerialCommand commands 
  SCmd.addCommand("S", receiveStop);      //  sendAck & sendFinalAck
  SCmd.addCommand("L", receiveLED);       //  sendAck & sendFinalAck
  SCmd.addCommand("T", recieveBuzzer);    //  sendAck & sendFinalAck
  SCmd.addCommand("M", receiveMovement);  //  sendAck & sendFinalAck
  SCmd.addCommand("H", receiveGesture);   //  sendAck & sendFinalAck
  SCmd.addCommand("K", receiveSing);      //  sendAck & sendFinalAck
  SCmd.addCommand("C", receiveTrims);     //  sendAck & sendFinalAck
  SCmd.addCommand("G", receiveServo);     //  sendAck & sendFinalAck
  SCmd.addCommand("R", receiveName);      //  sendAck & sendFinalAck
  SCmd.addCommand("E", requestName);
  SCmd.addCommand("D", requestDistance);
  SCmd.addCommand("N", requestNoise);
  //SCmd.addCommand("B", requestBattery); // doesn't function - needs work
  SCmd.addCommand("I", requestProgramId);
  SCmd.addDefaultHandler(receiveStop);



  //Otto wake up!
  OttoW.sing(S_connection);
  OttoW.home();


  //If Otto's name is '&' (factory name) means that is the first time this program is executed.
  //This first time, Otto mustn't do anything. Just born at the factory!
  //5 = EEPROM address that contains first name character
 /* if (EEPROM.read(5)==name_fac){ 

    EEPROM.put(5, name_fir); //From now, the name is '#'
    EEPROM.put(6, '\0'); 
    zowi.putMouth(culito);

    while(true){    
       delay(1000);
    }
  }  

*/
  //Send Otto name, programID & battery level.
  requestName();
  delay(50);
  //requestProgramId();
  delay(50);
  //requestBattery();
  
  //Checking battery
  //OttoLowBatteryAlarm();


 // Animation Uuuuuh - A little moment of initial surprise
 //-----
  for(int i=0; i<2; i++){
      for (int i=0;i<8;i++){
        if(buttonPushed){break;}  
        OttoW.putAnimationMouth(littleUuh,i);
        delay(150);
      }
  }
 //-----


  //Smile for a happy Otto :)
  if(!buttonPushed){ 
    OttoW.putMouth(smile);
    OttoW.sing(S_happy);
    delay(200);
  }


  //If Otto's name is '#' means that Otto hasn't been baptized
  //In this case, Otto does a longer greeting
  //5 = EEPROM address that contains first name character
  if (EEPROM.read(5)==name_fir){ 

    if(!buttonPushed){  
        OttoW.jump(1,700);
        delay(200); 
    }

    if(!buttonPushed){  
        OttoW.shakeLeg(1,T,1); 
    }  
    
    if(!buttonPushed){ 
        OttoW.putMouth(smallSurprise);
        OttoW.swing(2,800,20);  
        OttoW.home();
    }  
  }


  if(!buttonPushed){ 
    OttoW.putMouth(happyOpen);
  }

  previousMillis = millis();

}



///////////////////////////////////////////////////////////////////
//-- Principal Loop ---------------------------------------------//
///////////////////////////////////////////////////////////////////
void loop() {


  if (Serial.available()>0 && MODE!=4){

    MODE=4;
    OttoW.putMouth(happyOpen);

    //Disable Pin Interruptions
    disableInterrupt(PIN_SecondButton);
    disableInterrupt(PIN_ThirdButton);

    buttonPushed=false;
  }


  //First attemp to initial software
  if (buttonPushed){  

    OttoW.home();

    delay(100); //Wait for all buttons 
    OttoW.sing(S_buttonPushed);
    delay(200); //Wait for all buttons 

    if      ( buttonAPushed && !buttonBPushed){ MODE=1; OttoW.sing(S_mode1);}
    else if (!buttonAPushed && buttonBPushed) { MODE=2; OttoW.sing(S_mode2);}
    else if ( buttonAPushed && buttonBPushed) { MODE=3; OttoW.sing(S_mode3);} //else

    OttoW.putMouth(MODE);
 
    int showTime = 2000;
    while((showTime>0)){ //Wait to show the MODE number 
        
        showTime-=10;
        delay(10);
    }
     
    OttoW.putMouth(happyOpen);

    buttonPushed=false;
    buttonAPushed=false;
    buttonBPushed=false;

  }else{

    switch (MODE) {

      //-- MODE 0 - Otto is awaiting
      //---------------------------------------------------------
      case 0:
      
        //Every 80 seconds in this mode, Otto falls asleep 
        if (millis()-previousMillis>=60000){
            OttoSleeping_withInterrupts(); //ZZzzzzz...
            previousMillis=millis();         
        }

        break;
      

      //-- MODE 1 - Dance Mode!
      //---------------------------------------------------------
      case 1:
        
        randomDance=random(5,21); //5,20
        if((randomDance>14)&&(randomDance<19)){
            randomSteps=1;
            T=1600;
        }
        else{
            randomSteps=random(3,6); //3,5
            T=1000;
        }
        
        OttoW.putMouth(random(10,21));

        for (int i=0;i<randomSteps;i++){
            move(randomDance);
            if(buttonPushed){break;}
        }
        break;


      //-- MODE 2 - Obstacle detector mode
      //---------------------------------------------------------
      case 2:

        if(obstacleDetected){

            if(!buttonPushed){
              OttoW.putMouth(bigSurprise);
              OttoW.sing(S_surprise);
              //OttoW.jump(5, 500);
            }  

            if(!buttonPushed){
              OttoW.putMouth(confused);
              OttoW.sing(S_cuddly);
            }  

            //Otto takes two steps back
            OttoW.home(); // stop motors
             delay(500);
             if(buttonPushed){break;}
             int walk_R[2]={1370, 1520}; // speed, slow move backward, L servo, R servo ////1495  - 1430 deadband
             OttoW._moveServos(0, walk_R);  //moveServos(int time, int  servo_target[]);
 
            delay(300);
            obstacleDetector();
            delay(300);
            OttoW.home(); // stop motors

           //If there are no obstacles and no button is pressed, Otto shows a smile
           if((obstacleDetected==true)||(buttonPushed==true)){break;}            
           else{
              OttoW.putMouth(smile);
              delay(50);
              obstacleDetector();
           } 
            
           
           //If there are no obstacles and no button is pressed, Otto shows turns left
           
              if((obstacleDetected==true)||(buttonPushed==true)){break;}            
                OttoW.home(); // stop motors
                delay(200);
                int turn_1[2]={1390, 1390}; // speed, quickish move left, L servo, R servo ////1495   - 1430 deadband
                OttoW._moveServos(0, turn_1);  //moveServos(int time, int  servo_target[]); 
                obstacleDetector();
                delay(200);
                //OttoW._moveServos(0, turn_1);  //moveServos(int time, int  servo_target[]); 
                obstacleDetector();
                delay(200);
                OttoW.home();
           
            //If there are no obstacles and no button is pressed, Otto is happy
            if((obstacleDetected==true)||(buttonPushed==true)){break;}           
            else{
                OttoW.home();
                OttoW.putMouth(happyOpen);
                OttoW.sing(S_happy_short);
                delay(200);
            }     
        

        }else{

            //Otto walk straight
            int walk_F[2]={1535, 1400}; // speed, slow move forward, L servo, R servo ////1495   - 1430 deadband
            OttoW._moveServos(0, walk_F);  //moveServos(int time, int  servo_target[]);
            obstacleDetector();
            delay(100);
            obstacleDetector();
        }   

        break;


      //-- MODE 3 - Noise detector mode
      //---------------------------------------------------------  
      case 3:
        if (OttoW.getNoise()>=650){ //740
          
          delay(50);  //Wait for the possible 'lag' of the button interruptions. 
                      //Sometimes, the noise sensor detect the button before the interruption takes efect 
          
          if(!buttonPushed){

            OttoW.putMouth(bigSurprise);
            OttoW.sing(S_OhOoh);

            if(buttonPushed){break;}
            OttoW.putMouth(random(10,21));
            randomDance=random(5,21);
            move(randomDance);
            OttoW.home();
            delay(500); //Wait for possible noise of the servos while get home
          }
          
          if(!buttonPushed){OttoW.putMouth(happyOpen);}
        }
        break;
        

      //-- MODE 4 - OttoPAD or any Teleoperation mode (listening SerialPort) 
      //---------------------------------------------------------
      case 4:

        SCmd.readSerial();
        
        //If Otto is moving yet
        if (OttoW.getRestState()==false){  
          move(moveId);
        }
      
        break;      


      default:
          MODE=4;
          break;
    }

  } 

}  



///////////////////////////////////////////////////////////////////
//-- Functions --------------------------------------------------//
///////////////////////////////////////////////////////////////////

//-- Function executed when second button is pushed
void secondButtonPushed(){ 

    buttonAPushed=true;

    if(!buttonPushed){
        buttonPushed=true;
        OttoW.putMouth(smallSurprise);
    }    
}

//-- Function executed when third button is pushed
void thirdButtonPushed(){ 

    buttonBPushed=true;

    if(!buttonPushed){
        buttonPushed=true;
        OttoW.putMouth(smallSurprise);
    }
}


//-- Function to read distance sensor & to actualize obstacleDetected variable
void obstacleDetector(){

   int distance = OttoW.getDistance();

        if(distance<25){
          obstacleDetected = true;
          OttoW.home(); // stop motors
        }else{
          obstacleDetected = false;
        }
}


//-- Function to receive Stop command.
void receiveStop(){

    sendAck();
    OttoW.home();
    sendFinalAck();

}


//-- Function to receive LED commands
void receiveLED(){  

    //sendAck & stop if necessary
    sendAck();
    OttoW.home();

    //Examples of receiveLED Bluetooth commands
    //L 000000001000010100100011000000000
    //L 001111111111111111111111111111111 (todos los LED encendidos)
    unsigned long int matrix;
    char *arg;
    char *endstr;
    arg=SCmd.next();
    //Serial.println (arg);
    if (arg != NULL) {
      matrix=strtoul(arg,&endstr,2);    // Converts a char string to unsigned long integer
      OttoW.putMouth(matrix,false);
    }else{
      OttoW.putMouth(xMouth);
      delay(2000);
      OttoW.clearMouth();
    }

    sendFinalAck();

}


//-- Function to receive buzzer commands
void recieveBuzzer(){
  
    //sendAck & stop if necessary
    sendAck();
    OttoW.home(); 

    bool error = false; 
    int frec;
    int duration; 
    char *arg; 
    
    arg = SCmd.next(); 
    if (arg != NULL) { frec=atoi(arg); }    // Converts a char string to an integer   
    else {error=true;}
    
    arg = SCmd.next(); 
    if (arg != NULL) { duration=atoi(arg); } // Converts a char string to an integer  
    else {error=true;}

    if(error==true){

      OttoW.putMouth(xMouth);
      delay(2000);
      OttoW.clearMouth();

    }else{ 

      OttoW._tone(frec, duration, 1);   
    }

    sendFinalAck();

}


//-- Function to receive TRims commands
void receiveTrims(){  

    //sendAck & stop if necessary
    sendAck();
    OttoW.home(); 

    int trim_YL,trim_YR;

    //Definition of Servo Bluetooth command
    //C trim_YL trim_YR trim_RL trim_RR
    //Examples of receiveTrims Bluetooth commands
    //C 20 0 -8 3
    bool error = false;
    char *arg;
    arg=SCmd.next();
    if (arg != NULL) { trim_YL=atoi(arg); }    // Converts a char string to an integer   
    else {error=true;}

    arg = SCmd.next(); 
    if (arg != NULL) { trim_YR=atoi(arg); }    // Converts a char string to an integer  
    else {error=true;}

    arg = SCmd.next(); 
    
    
    if(error==true){

      OttoW.putMouth(xMouth);
      delay(2000);
      OttoW.clearMouth();

    }else{ //Save it on EEPROM
      OttoW.setTrims(trim_YL, trim_YR);
      OttoW.saveTrimsOnEEPROM(); //Uncomment this only for one upload when you finaly set the trims.
    } 

    sendFinalAck();

}


//-- Function to receive Servo commands
void receiveServo(){  

    sendAck(); 
    moveId = 30;

    //Definition of Servo Bluetooth command
    //G  servo_YL servo_YR servo_RL servo_RR 
    //Example of receiveServo Bluetooth commands
    //G 90 85 96 78 
    bool error = false;
    char *arg;
    int servo_YL,servo_YR,servo_RL,servo_RR;

    arg=SCmd.next();
    if (arg != NULL) { servo_YL=atoi(arg); }    // Converts a char string to an integer   
    else {error=true;}

    arg = SCmd.next(); 
    if (arg != NULL) { servo_YR=atoi(arg); }    // Converts a char string to an integer  
    else {error=true;}

    arg = SCmd.next(); 
    if (arg != NULL) { servo_RL=atoi(arg); }    // Converts a char string to an integer  
    else {error=true;}

    arg = SCmd.next(); 
    if (arg != NULL) { servo_RR=atoi(arg); }    // Converts a char string to an integer  
    else {error=true;}
    
    if(error==true){

      OttoW.putMouth(xMouth);
      delay(2000);
      OttoW.clearMouth();

    }else{ //Update Servo:

      int servoPos[4]={servo_YL, servo_YR, servo_RL, servo_RR}; 
      OttoW._moveServos(200, servoPos);   //Move 200ms
      
    }

    sendFinalAck();

}


//-- Function to receive movement commands
void receiveMovement(){

    sendAck();

    if (OttoW.getRestState()==true){
        OttoW.setRestState(false);
    }

    //Definition of Movement Bluetooth commands
    //M  MoveID  T   MoveSize  
    char *arg; 
    arg = SCmd.next(); 
    if (arg != NULL) {moveId=atoi(arg);}
    else{
      OttoW.putMouth(xMouth);
      delay(2000);
      OttoW.clearMouth();
      moveId=0; //stop
    }
    
    arg = SCmd.next(); 
    if (arg != NULL) {T=atoi(arg);}
    else{
      T=1000;
    }

    arg = SCmd.next(); 
    if (arg != NULL) {moveSize=atoi(arg);}
    else{
      moveSize =15;
    }
}


//-- Function to execute the right movement according the movement command received.
void move(int moveId){

  bool manualMode = false;

  switch (moveId) {
    case 0:
      //OttoW.home();
      break;
    case 1: //M 1 1000 
      //OttoW.walk(1,T,1);
      break;
    case 2: //M 2 1000 
      //OttoW.walk(1,T,-1);
      break;
    case 3: //M 3 1000 
      //OttoW.turn(1,T,1);
      break;
    case 4: //M 4 1000 
      //OttoW.turn(1,T,-1);
      break;
    case 5: //M 5 1000 30 
      //OttoW.updown(1,T,moveSize);
      break;
    case 6: //M 6 1000 30
      //OttoW.moonwalker(1,T,moveSize,1);
      break;
    case 7: //M 7 1000 30
      //OttoW.moonwalker(1,T,moveSize,-1);
      break;
    case 8: //M 8 1000 30
      //OttoW.swing(1,T,moveSize);
      break;
    case 9: //M 9 1000 30 
      //OttoW.crusaito(1,T,moveSize,1);
      break;
    case 10: //M 10 1000 30 
      //OttoW.crusaito(1,T,moveSize,-1);
      break;
    case 11: //M 11 1000 
      //OttoW.jump(1,T);
      break;
    case 12: //M 12 1000 30 
     //OttoW.flapping(1,T,moveSize,1);
      break;
    case 13: //M 13 1000 30
      //OttoW.flapping(1,T,moveSize,-1);
      break;
    case 14: //M 14 1000 20
      //OttoW.tiptoeSwing(1,T,moveSize);
      break;
    case 15: //M 15 500 
      //OttoW.bend(1,T,1);
      break;
    case 16: //M 16 500 
      //OttoW.bend(1,T,-1);
      break;
    case 17: //M 17 500 
      //OttoW.shakeLeg(1,T,1);
      break;
    case 18: //M 18 500 
      //OttoW.shakeLeg(1,T,-1);
      break;
    case 19: //M 19 500 20
      //OttoW.jitter(1,T,moveSize);
      break;
    case 20: //M 20 500 15
      //OttoW.ascendingTurn(1,T,moveSize);
      break;
    default:
        manualMode = true;
      break;
  }

  if(!manualMode){
    sendFinalAck();
  }
       
}


//-- Function to receive gesture commands
void receiveGesture(){

    //sendAck & stop if necessary
    sendAck();
    OttoW.home(); 

    //Definition of Gesture Bluetooth commands
    //H  GestureID  
    int gesture = 0;
    char *arg; 
    arg = SCmd.next(); 
    if (arg != NULL) {gesture=atoi(arg);}
    else 
    {
      //Otto.putMouth(xMouth);
      delay(2000);
      //Otto.clearMouth();
    }

    switch (gesture) {
      case 1: //H 1 
        OttoW.playGesture(OttoHappy);
        break;
      case 2: //H 2 
        OttoW.playGesture(OttoSuperHappy);
        break;
      case 3: //H 3 
        OttoW.playGesture(OttoSad);
        break;
      case 4: //H 4 
        OttoW.playGesture(OttoSleeping);
        break;
      case 5: //H 5  
        OttoW.playGesture(OttoFart);
        break;
      case 6: //H 6 
        OttoW.playGesture(OttoConfused);
        break;
      case 7: //H 7 
        OttoW.playGesture(OttoLove);
        break;
      case 8: //H 8 
        OttoW.playGesture(OttoAngry);
        break;
      case 9: //H 9  
        OttoW.playGesture(OttoFretful);
        break;
      case 10: //H 10
        OttoW.playGesture(OttoMagic);
        break;  
      case 11: //H 11
        OttoW.playGesture(OttoWave);
        break;   
      case 12: //H 12
        OttoW.playGesture(OttoVictory);
        break; 
      case 13: //H 13
        OttoW.playGesture(OttoFail);
        break;         
      default:
        break;
    }

    sendFinalAck();
}

//-- Function to receive sing commands
void receiveSing(){

    //sendAck & stop if necessary
    sendAck();
    OttoW.home(); 

    //Definition of Sing Bluetooth commands
    //K  SingID    
    int sing = 0;
    char *arg; 
    arg = SCmd.next(); 
    if (arg != NULL) {sing=atoi(arg);}
    else 
    {
     // Otto.putMouth(xMouth);
      delay(2000);
      //Otto.clearMouth();
    }

    switch (sing) {
      case 1: //K 1 
        OttoW.sing(S_connection);
        break;
      case 2: //K 2 
        OttoW.sing(S_disconnection);
        break;
      case 3: //K 3 
        OttoW.sing(S_surprise);
        break;
      case 4: //K 4 
        OttoW.sing(S_OhOoh);
        break;
      case 5: //K 5  
        OttoW.sing(S_OhOoh2);
        break;
      case 6: //K 6 
        OttoW.sing(S_cuddly);
        break;
      case 7: //K 7 
        OttoW.sing(S_sleeping);
        break;
      case 8: //K 8 
        OttoW.sing(S_happy);
        break;
      case 9: //K 9  
        OttoW.sing(S_superHappy);
        break;
      case 10: //K 10
        OttoW.sing(S_happy_short);
        break;  
      case 11: //K 11
        OttoW.sing(S_sad);
        break;   
      case 12: //K 12
        OttoW.sing(S_confused);
        break; 
      case 13: //K 13
        OttoW.sing(S_fart1);
        break;
      case 14: //K 14
        OttoW.sing(S_fart2);
        break;
      case 15: //K 15
        OttoW.sing(S_fart3);
        break;    
      case 16: //K 16
        OttoW.sing(S_mode1);
        break; 
      case 17: //K 17
        OttoW.sing(S_mode2);
        break; 
      case 18: //K 18
        OttoW.sing(S_mode3);
        break;   
      case 19: //K 19
        OttoW.sing(S_buttonPushed);
        break;                      
      default:
        break;
    }

    sendFinalAck();
}


//-- Function to receive Name command
void receiveName(){

    //sendAck & stop if necessary
    sendAck();
    OttoW.home(); 

    char newOttoName[11] = "";  //Variable to store data read from Serial.
    int eeAddress = 5;          //Location we want the data to be in EEPROM.
    char *arg; 
    arg = SCmd.next(); 
    
    if (arg != NULL) {

      //Complete newOttoName char string
      int k = 0;
      while((*arg) && (k<11)){ 
          newOttoName[k]=*arg++;
          k++;
      }
      
      EEPROM.put(eeAddress, newOttoName); 
    }
    else 
    {
      //Otto.putMouth(xMouth);
      delay(2000);
      //Otto.clearMouth();
    }

    sendFinalAck();

}


//-- Function to send Otto's name
void requestName(){

    OttoW.home(); //stop if necessary

    char actualOttoName[11]= "";  //Variable to store data read from EEPROM.
    int eeAddress = 5;            //EEPROM address to start reading from

    //Get the float data from the EEPROM at position 'eeAddress'
    EEPROM.get(eeAddress, actualOttoName);

    Serial.print(F("&&"));
    Serial.print(F("E "));
    Serial.print(actualOttoName);
    Serial.println(F("%%"));
    Serial.flush();
}


//-- Function to send ultrasonic sensor measure (distance in "cm")
void requestDistance(){

    OttoW.home();  //stop if necessary  

    int distance = OttoW.getDistance();
    Serial.print(F("&&"));
    Serial.print(F("D "));
    Serial.print(distance);
    Serial.println(F("%%"));
    Serial.flush();
}


//-- Function to send noise sensor measure
void requestNoise(){

    OttoW.home();  //stop if necessary

    int microphone= OttoW.getNoise(); //analogRead(PIN_NoiseSensor);
    Serial.print(F("&&"));
    Serial.print(F("N "));
    Serial.print(microphone);
    Serial.println(F("%%"));
    Serial.flush();
}


//-- Function to send battery voltage percent
void requestBattery(){

    OttoW.home();  //stop if necessary

    //The first read of the batery is often a wrong reading, so we will discard this value. 
    double batteryLevel = OttoW.getBatteryLevel();

    Serial.print(F("&&"));
    Serial.print(F("B "));
    Serial.print(batteryLevel);
    Serial.println(F("%%"));
    Serial.flush();
}


//-- Function to send program ID
void requestProgramId(){

    OttoW.home();   //stop if necessary

    Serial.print(F("&&"));
    Serial.print(F("I "));
    Serial.print(programID);
    Serial.println(F("%%"));
    Serial.flush();
}


//-- Function to send Ack comand (A)
void sendAck(){

  delay(30);

  Serial.print(F("&&"));
  Serial.print(F("A"));
  Serial.println(F("%%"));
  Serial.flush();
}


//-- Function to send final Ack comand (F)
void sendFinalAck(){

  delay(30);

  Serial.print(F("&&"));
  Serial.print(F("F"));
  Serial.println(F("%%"));
  Serial.flush();
}



//-- Functions with animatics
//--------------------------------------------------------

void OttoLowBatteryAlarm(){

    double batteryLevel = OttoW.getBatteryLevel();

    if(batteryLevel<45){
        
      while(!buttonPushed){

          OttoW.putMouth(thunder);
          OttoW.bendTones (880, 2000, 1.04, 8, 3);  //A5 = 880
          
          delay(30);

          OttoW.bendTones (2000, 880, 1.02, 8, 3);  //A5 = 880
          OttoW.clearMouth();
          delay(500);
      } 
    }
}

void OttoSleeping_withInterrupts(){


  for(int i=0; i<4;i++){

    if(buttonPushed){break;}
      OttoW.putAnimationMouth(dreamMouth,0);
      OttoW.bendTones (100, 200, 1.04, 10, 10);
    
    if(buttonPushed){break;}
      OttoW.putAnimationMouth(dreamMouth,1);
      OttoW.bendTones (200, 300, 1.04, 10, 10);  

    if(buttonPushed){break;}
      OttoW.putAnimationMouth(dreamMouth,2);
      OttoW.bendTones (300, 500, 1.04, 10, 10);   

      int bedPos_1[2]={83, 95}; // speed, slow move forward, L servo, R servo
    OttoW._moveServos(0, bedPos_1);  //moveServos(int time, int  servo_target[]);
    delay(500);
    
    if(buttonPushed){break;}
      OttoW.putAnimationMouth(dreamMouth,1);
      OttoW.bendTones (400, 250, 1.04, 10, 1); 

    if(buttonPushed){break;}
      OttoW.putAnimationMouth(dreamMouth,0);
      OttoW.bendTones (250, 100, 1.04, 10, 1); 
    
    int bedPos_3[2]={95, 83}; // speed, slow move backward, L servo, R servo
    OttoW._moveServos(0, bedPos_3);  //moveServos(int time, int  servo_target[]);
    delay(500);
  } 

  if(!buttonPushed){
    OttoW.putMouth(lineMouth);
    OttoW.sing(S_cuddly);
  }

  OttoW.home();
  if(!buttonPushed){OttoW.putMouth(happyOpen);}  

}
